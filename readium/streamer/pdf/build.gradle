/*
 * Copyright 2018 Readium Foundation. All rights reserved.
 * Use of this source code is governed by the BSD-style license
 * available in the top-level LICENSE file of the project.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

android {
    compileSdkVersion 31
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        allWarningsAsErrors = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.readium'
                artifactId = 'readium-streamer-pdf'

                artifact sourcesJar
                artifact javadocsJar
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    api project(':readium:shared')
    api project(':readium:streamer:core')

    final JACKSON_VERSION = '2.12.4'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "com.fasterxml.jackson.core:jackson-annotations:$JACKSON_VERSION"
    implementation "com.fasterxml.jackson.core:jackson-core:$JACKSON_VERSION"
    implementation "com.fasterxml.jackson.core:jackson-databind:$JACKSON_VERSION"
    implementation "com.github.barteksc:pdfium-android:1.7.1"
    implementation "com.jakewharton.timber:timber:4.7.1"
    //noinspection GradleDependency
    implementation ("com.github.edrlab.nanohttpd:nanohttpd:master-SNAPSHOT") {
        exclude group: 'org.parboiled'
    }
    //noinspection GradleDependency
    implementation ("com.github.edrlab.nanohttpd:nanohttpd-nanolets:master-SNAPSHOT") {
        exclude group: 'org.parboiled'
    }
    //AM NOTE: conflicting support libraries, excluding these
    implementation("com.mcxiaoke.koi:core:0.5.5") {
        exclude module: 'support-v4'
    }
    // useful extensions (only ~100k)
    implementation("com.mcxiaoke.koi:async:0.5.5") {
        exclude module: 'support-v4'
    }
    implementation "joda-time:joda-time:2.10.13"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"

    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    testImplementation 'androidx.test:core-ktx:1.4.0'
    testImplementation "junit:junit:4.13.2"
    testImplementation "net.sf.kxml:kxml2:2.3.0"
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:1.5.31"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0"
    // Latest version of org.json is incompatible with the one bundled in Android, breaking the tests.
    //noinspection GradleDependency
    testImplementation "org.json:json:20200518"
    testImplementation "org.mockito:mockito-core:3.3.3"
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation "xmlpull:xmlpull:1.1.3.1"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
            "-Xopt-in=org.readium.r2.shared.InternalReadiumApi",
        ]
    }
}
