/*
 * Copyright 2018 Readium Foundation. All rights reserved.
 * Use of this source code is governed by the BSD-style license
 * available in the top-level LICENSE file of the project.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

android {
    // FIXME: This doesn't pass the lint because some resources don't start with r2_ yet. We need to rename all resources for the next major version.
//    resourcePrefix "r2_"

    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
    buildFeatures {
        viewBinding true
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.readium'
                artifactId = 'readium-navigator-core'

                artifact sourcesJar
                artifact javadocsJar
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    api project(':readium:shared')

    implementation 'androidx.activity:activity-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "androidx.browser:browser:1.3.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation "androidx.legacy:legacy-support-core-ui:1.0.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.3.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.media:media:1.4.3"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.webkit:webkit:1.4.0"
    // Needed to avoid a crash with API 31, see https://stackoverflow.com/a/69152986/1474476
    implementation 'androidx.work:work-runtime-ktx:2.7.0'
    implementation "com.duolingo.open:rtl-viewpager:1.0.3"
    api "com.github.barteksc:android-pdf-viewer:2.8.2"
    // ChrisBane/PhotoView ( for the Zoom handling )
    implementation "com.github.chrisbanes:PhotoView:2.3.0"
    // ExoPlayer is used by the Audio Navigator.
    api 'com.google.android.exoplayer:exoplayer-core:2.15.1'
    api 'com.google.android.exoplayer:exoplayer-ui:2.15.1'
    api 'com.google.android.exoplayer:extension-mediasession:2.15.1'
    api 'com.google.android.exoplayer:extension-workmanager:2.15.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.shopgun.android:utils:1.0.9"
    implementation 'joda-time:joda-time:2.10.13'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
    // AM NOTE: needs to stay this version for now (June 24,2020)
    //noinspection GradleDependency
    implementation 'org.jsoup:jsoup:1.14.3'

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
            "-Xopt-in=org.readium.r2.shared.InternalReadiumApi",
        ]
    }
}
