/*
 * Module: r2-testapp-kotlin
 * Developers: Aferdita Muriqi, Cl√©ment Baumann
 *
 * Copyright (c) 2018. European Digital Reading Lab. All rights reserved.
 * Licensed to the Readium Foundation under one or more contributor license agreements.
 * Use of this source code is governed by a BSD-style license which is detailed in the
 * LICENSE file present in the project repository where this source code is maintained.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
group = 'com.github.readium'


int major = 2
int minor = 2
int patch = 1
int build = 29
String type = ""

def version = "$major.$minor.$patch"
boolean appendBuild = build != 0
if (appendBuild || !type.empty) {
    version += "-$type"
    if (appendBuild) {
        version += build
    }
}

project.ext.versionName = version
project.ext.versionCode = 1_000_000 * major + 10_000 * minor + 100 * patch + build


android {

    compileSdkVersion 31
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31

        applicationId "org.readium.r2reader"

        versionCode project.ext.versionCode
        versionName project.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    final READIUM_VERSION = '2.1.0'
    if (findProject(':r2-shared')) {
        implementation project(':r2-shared')
    } else {
        implementation "com.github.readium:r2-shared-kotlin:$READIUM_VERSION"
    }
    if (findProject(':r2-navigator')) {
        implementation project(':r2-navigator')
    } else {
        implementation "com.github.readium:r2-navigator-kotlin:$READIUM_VERSION"
    }
    if (findProject(':r2-streamer')) {
        implementation project(':r2-streamer')
    } else {
        implementation "com.github.readium:r2-streamer-kotlin:$READIUM_VERSION"
    }
    if (findProject(':r2-opds')) {
        implementation project(':r2-opds')
    } else {
        implementation "com.github.readium:r2-opds-kotlin:$READIUM_VERSION"
    }
    if (findProject(':r2-lcp')) {
        implementation project(':r2-lcp')
    } else {
        implementation "com.github.readium:r2-lcp-kotlin:$READIUM_VERSION"
    }

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation "androidx.activity:activity-ktx:1.3.1"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.browser:browser:1.3.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.0"
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation "androidx.paging:paging-runtime-ktx:3.0.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.webkit:webkit:1.4.0"
    //noinspection GradleDependency
    implementation ("com.github.edrlab.nanohttpd:nanohttpd:master-SNAPSHOT") {
        exclude group: 'org.parboiled'
    }
    //noinspection GradleDependency
    implementation ("com.github.edrlab.nanohttpd:nanohttpd-nanolets:master-SNAPSHOT") {
        exclude group: 'org.parboiled'
    }
    implementation "com.google.android.material:material:1.4.0"
    implementation "com.jakewharton.timber:timber:4.7.1"
    // AM NOTE: needs to stay this version for now (June 24,2020)
    //noinspection GradleDependency
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation "joda-time:joda-time:2.10.10"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0"
    // AM NOTE: needs to stay this version for now (June 24,2020)
    //noinspection GradleDependency
    implementation 'org.jsoup:jsoup:1.13.1'

    // Room database
    final room_version = '2.3.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt "androidx.lifecycle:lifecycle-compiler:2.3.1"

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "junit:junit:4.13.2"

    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"

}
