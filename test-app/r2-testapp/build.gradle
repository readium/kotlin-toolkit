/*
 * Module: r2-testapp-kotlin
 * Developers: Aferdita Muriqi, Cl√©ment Baumann
 *
 * Copyright (c) 2018. European Digital Reading Lab. All rights reserved.
 * Licensed to the Readium Foundation under one or more contributor license agreements.
 * Use of this source code is governed by a BSD-style license which is detailed in the
 * LICENSE file present in the project repository where this source code is maintained.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.readium'


int major = 2
int minor = 2
int patch = 0
int build = 27
String type = ""

def version = "$major.$minor.$patch"
boolean appendBuild = build != 0
if (appendBuild || !type.empty) {
    version += "-$type"
    if (appendBuild) {
        version += build
    }
}

project.ext.versionName = version
project.ext.versionCode = 1_000_000 * major + 10_000 * minor + 100 * patch + build


android {

    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        applicationId "org.readium.r2reader"

        versionCode project.ext.versionCode
        versionName project.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    final READIUM_VERSION = '2.0.0-beta.2'
    if (findProject(':r2-shared')) {
        implementation project(':r2-shared')
    } else {
        implementation "com.github.readium:r2-shared-kotlin:$READIUM_VERSION"
    }
    if (findProject(':r2-navigator')) {
        implementation project(':r2-navigator')
    } else {
        implementation "com.github.readium:r2-navigator-kotlin:$READIUM_VERSION"
    }
    if (findProject(':r2-streamer')) {
        implementation project(':r2-streamer')
    } else {
        implementation "com.github.readium:r2-streamer-kotlin:$READIUM_VERSION"
    }
    if (findProject(':r2-opds')) {
        implementation project(':r2-opds')
    } else {
        implementation "com.github.readium:r2-opds-kotlin:$READIUM_VERSION"
    }
    if (findProject(':r2-lcp')) {
        implementation project(':r2-lcp')
    } else {
        implementation "com.github.readium:r2-lcp-kotlin:$READIUM_VERSION"
    }

    implementation "androidx.activity:activity-ktx:1.2.0-beta01"
    implementation "androidx.appcompat:appcompat:1.3.0-alpha02"
    implementation "androidx.browser:browser:1.2.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.3"
    implementation "androidx.fragment:fragment-ktx:1.3.0-beta01"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0-beta01"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.0-beta01"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0-beta01"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.webkit:webkit:1.3.0"
    implementation "com.commonsware.cwac:merge:1.1.2"
    implementation "com.duolingo.open:rtl-viewpager:1.0.3"
    implementation "com.github.abacritt:sectionedmergeadapter:0.2"
    //noinspection GradleDependency
    implementation ("com.github.edrlab.nanohttpd:nanohttpd:master-SNAPSHOT") {
        exclude group: 'org.parboiled'
    }
    //noinspection GradleDependency
    implementation ("com.github.edrlab.nanohttpd:nanohttpd-nanolets:master-SNAPSHOT") {
        exclude group: 'org.parboiled'
    }
    implementation "com.github.kittinunf.fuel:fuel:2.2.2"
    implementation "com.github.kittinunf.fuel:fuel-android:2.2.2"
    implementation "com.google.android.material:material:1.3.0-alpha03"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation("com.mcxiaoke.koi:core:0.5.5") {
        exclude module: 'support-v4'
    }
    implementation("com.mcxiaoke.koi:async:0.5.5") {
        exclude module: 'support-v4'
    }
    // AM NOTE: needs to stay this version for now (June 24,2020)
    //noinspection GradleDependency
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "joda-time:joda-time:2.10.5"
    final KOVENANT_VERSION = '3.3.0'
    implementation "nl.komponents.kovenant:kovenant:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-android:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-combine:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-core:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-functional:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-jvm:$KOVENANT_VERSION"
    implementation "org.jetbrains.anko:anko-appcompat-v7-coroutines:0.10.8"
    implementation "org.jetbrains.anko:anko-appcompat-v7:0.10.8"
    implementation "org.jetbrains.anko:anko-commons:0.10.8"
    implementation "org.jetbrains.anko:anko-design:0.10.8"
    implementation "org.jetbrains.anko:anko-recyclerview-v7:0.10.8"
    implementation "org.jetbrains.anko:anko-sdk25-coroutines:0.10.8"
    implementation "org.jetbrains.anko:anko-sdk25:0.10.8"
    implementation "org.jetbrains.anko:anko-sqlite:0.10.8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    // AM NOTE: needs to stay this version for now (June 24,2020)
    //noinspection GradleDependency
    implementation "org.jsoup:jsoup:1.10.3"
    implementation "org.slf4j:slf4j-nop:1.7.30"

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "junit:junit:4.13.1"

    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test:runner:1.3.0"

}
