/*
 * Module: r2-testapp-kotlin
 * Developers: Aferdita Muriqi, Cl√©ment Baumann
 *
 * Copyright (c) 2018. European Digital Reading Lab. All rights reserved.
 * Licensed to the Readium Foundation under one or more contributor license agreements.
 * Use of this source code is governed by a BSD-style license which is detailed in the
 * LICENSE file present in the project repository where this source code is maintained.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.readium'


int major = 2
int minor = 2
int patch = 0
int build = 24
String type = ""

def version = "$major.$minor.$patch"
boolean appendBuild = build != 0
if (appendBuild || !type.empty) {
    version += "-$type"
    if (appendBuild) {
        version += build
    }
}

project.ext.versionName = version
project.ext.versionCode = 1_000_000 * major + 10_000 * minor + 100 * patch + build


android {

    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        applicationId "org.readium.r2reader"

        versionCode project.ext.versionCode
        versionName project.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
            if (findProject(':r2-lcp')) {
                java.srcDirs = ['src/main/java', 'src/withlcp/java']
            } else {
                java.srcDirs = ['src/main/java', 'src/nondrm/java']
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.6"
    implementation 'com.jakewharton.timber:timber:4.7.1'

    if (findProject(':r2-shared')) {
        implementation project(':r2-shared')
    } else {
        implementation "com.github.readium:r2-shared-kotlin:2.0.0-alpha.1"
    }
    if (findProject(':r2-navigator')) {
        implementation project(':r2-navigator')
    } else {
        implementation "com.github.readium:r2-navigator-kotlin:2.0.0-alpha.1"
    }
    if (findProject(':r2-streamer')) {
        implementation project(':r2-streamer')
    } else {
        implementation "com.github.readium:r2-streamer-kotlin:2.0.0-alpha.1"
    }
    if (findProject(':r2-opds')) {
        implementation project(':r2-opds')
    } else {
        implementation "com.github.readium:r2-opds-kotlin:2.0.0-alpha.1"
    }
    if (findProject(':r2-lcp')) {
        implementation project(':r2-lcp')
    }


    implementation('com.mcxiaoke.koi:core:0.5.5') {
        exclude module: 'support-v4'
    }
    implementation('com.mcxiaoke.koi:async:0.5.5') {
        exclude module: 'support-v4'
    }

    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0-alpha07'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.0-alpha07'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0-alpha07'
    implementation 'androidx.webkit:webkit:1.2.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation ('com.github.edrlab.nanohttpd:nanohttpd-nanolets:2.3.2') {
        exclude group: 'org.parboiled'
    }

    implementation ('com.github.edrlab.nanohttpd:nanohttpd:2.3.2') {
        exclude group: 'org.parboiled'
    }

    // AM NOTE: needs to stay this version for now (June 24,2020)
    //noinspection GradleDependency
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'org.jetbrains.anko:anko-design:0.10.8'

    // For SnackBars
    implementation 'org.jetbrains.anko:anko-commons:0.10.8'

    // Anko Layouts
    implementation 'org.jetbrains.anko:anko-sdk25:0.10.8'

    // sdk15, sdk19, sdk21, sdk23 are also available
    implementation 'org.jetbrains.anko:anko-appcompat-v7:0.10.8'
    implementation 'org.jetbrains.anko:anko-recyclerview-v7:0.10.8'

    // Coroutine listeners for Anko Layouts
    implementation 'org.jetbrains.anko:anko-sdk25-coroutines:0.10.8'
    implementation 'org.jetbrains.anko:anko-appcompat-v7-coroutines:0.10.8'

    // Anko SQLite
    implementation 'org.jetbrains.anko:anko-sqlite:0.10.8'
    implementation 'com.github.kittinunf.fuel:fuel:2.2.2'

    //for JVM
    implementation 'com.github.kittinunf.fuel:fuel-android:2.2.2'

    // async functions (only ~70k)
    final KOVENANT_VERSION = '3.3.0'
    implementation "nl.komponents.kovenant:kovenant:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-core:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-android:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-combine:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-jvm:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-functional:$KOVENANT_VERSION"

    implementation 'org.zeroturnaround:zt-zip:1.14'
    implementation 'joda-time:joda-time:2.10.5'
    implementation 'org.slf4j:slf4j-nop:1.7.30'

    implementation 'com.duolingo.open:rtl-viewpager:1.0.3'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation 'com.commonsware.cwac:merge:1.1.2'
    implementation 'com.github.abacritt:sectionedmergeadapter:0.2'

    // AM NOTE: needs to stay this version for now (June 24,2020)
    //noinspection GradleDependency
    implementation 'org.jsoup:jsoup:1.10.3'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

}
