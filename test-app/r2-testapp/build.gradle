/*
 * Module: r2-testapp-kotlin
 * Developers: Aferdita Muriqi, Cl√©ment Baumann
 *
 * Copyright (c) 2018. European Digital Reading Lab. All rights reserved.
 * Licensed to the Readium Foundation under one or more contributor license agreements.
 * Use of this source code is governed by a BSD-style license which is detailed in the
 * LICENSE file present in the project repository where this source code is maintained.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.readium'


int major = 2
int minor = 0
int patch = 2
int build = 2
String type = ""

def version = "$major.$minor.$patch"
boolean appendBuild = build != 0
if (appendBuild || !type.empty) {
    version += "-$type"
    if (appendBuild) {
        version += build
    }
}

project.ext.versionName = version
project.ext.versionCode = 1_000_000 * major + 10_000 * minor + 100 * patch + build


android {
    flavorDimensions "testapp"

    compileSdkVersion 27
    signingConfigs {
        release {
            keyAlias 'EDRLab'
            keyPassword '###         '
            storeFile file('/###')
            storePassword '###         '
        }
    }
    defaultConfig {
        applicationId "org.readium.r2reader"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode project.ext.versionCode
        versionName project.ext.versionName
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
    productFlavors {
        intTestapp {
            dimension "testapp"
        }
        intTestappWithLcp {
            dimension "testapp"
        }
        devTestapp {
            dimension "testapp"
        }
        devTestappWithLcp {
            dimension "testapp"
        }
    }

    // default build flavour
    defaultPublishConfig "intTestappDebug"

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
        }
        intTestapp {
            java.srcDirs = ['src/main/java', 'src/nondrm/java']
        }
        devTestapp {
            java.srcDirs = ['src/main/java', 'src/nondrm/java']
        }
        intTestappWithLcp {
            java.srcDirs = ['src/main/java', 'src/withlcp/java']
        }
        devTestappWithLcp {
            java.srcDirs = ['src/main/java', 'src/withlcp/java']
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    if (findProject(':r2-navigator')) {
        devTestappImplementation project(':r2-navigator')
        devTestappWithLcpImplementation project(':r2-navigator')
    }
    if (findProject(':r2-streamer')) {
        devTestappImplementation project(':r2-streamer')
        devTestappWithLcpImplementation project(':r2-streamer')
    }
    if (findProject(':r2-shared')) {
        devTestappImplementation project(':r2-shared')
        devTestappWithLcpImplementation project(':r2-shared')
    }
    if (findProject(':r2-opds')) {
        devTestappImplementation project(':r2-opds')
        devTestappWithLcpImplementation project(':r2-opds')
    }
    if (findProject(':r2-lcp')) {
        devTestappWithLcpImplementation project(':r2-lcp')
        intTestappWithLcpImplementation project(':r2-lcp')
    }

    intTestappImplementation "com.github.readium:r2-shared-kotlin:$r2branch-SNAPSHOT"
    intTestappImplementation "com.github.readium:r2-streamer-kotlin:$r2branch-SNAPSHOT"
    intTestappImplementation "com.github.readium:r2-navigator-kotlin:$r2branch-SNAPSHOT"
    intTestappImplementation "com.github.readium:r2-opds-kotlin:$r2branch-SNAPSHOT"

    intTestappWithLcpImplementation "com.github.readium:r2-shared-kotlin:$r2branch-SNAPSHOT"
    intTestappWithLcpImplementation "com.github.readium:r2-streamer-kotlin:$r2branch-SNAPSHOT"
    intTestappWithLcpImplementation "com.github.readium:r2-navigator-kotlin:$r2branch-SNAPSHOT"
    intTestappWithLcpImplementation "com.github.readium:r2-opds-kotlin:$r2branch-SNAPSHOT"

    implementation('com.mcxiaoke.koi:core:0.5.5') {
        exclude module: 'support-v4'
    }
    implementation('com.mcxiaoke.koi:async:0.5.5') {
        exclude module: 'support-v4'
    }

    //    implementation 'androidx.core:core-ktx:0.3'
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'org.nanohttpd:nanohttpd:2.3.2-SNAPSHOT'
    implementation 'org.nanohttpd:nanohttpd-nanolets:2.3.2-SNAPSHOT'

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'org.jetbrains.anko:anko-design:0.10.5'

    // For SnackBars
    implementation 'org.jetbrains.anko:anko-commons:0.10.8'

    // Anko Layouts
    implementation 'org.jetbrains.anko:anko-sdk25:0.10.5'

    // sdk15, sdk19, sdk21, sdk23 are also available
    implementation 'org.jetbrains.anko:anko-appcompat-v7:0.10.5'
    implementation 'org.jetbrains.anko:anko-recyclerview-v7:0.10.5'

    // Coroutine listeners for Anko Layouts
    implementation 'org.jetbrains.anko:anko-sdk25-coroutines:0.10.8'
    implementation 'org.jetbrains.anko:anko-appcompat-v7-coroutines:0.10.8'

    // Anko SQLite
    implementation 'org.jetbrains.anko:anko-sqlite:0.10.8'
    implementation 'com.github.kittinunf.fuel:fuel:1.16.0'

    //for JVM
    implementation 'com.github.kittinunf.fuel:fuel-android:1.16.0'

    //for Android
    //    implementation 'com.github.kittinunf.fuel:fuel-livedata:1.12.1' //for LiveData support
    //    implementation 'com.github.kittinunf.fuel:fuel-rxjava:1.12.1' //for RxJava support
    //    implementation 'com.github.kittinunf.fuel:fuel-gson:1.12.1' //for Gson support
    //    implementation 'com.github.kittinunf.fuel:fuel-jackson:1.12.1' //for Jackson support
    //    implementation 'com.github.kittinunf.fuel:fuel-moshi:1.12.1' //for Moshi support

    // async functions (only ~70k)
    final KOVENANT_VERSION = '3.3.0'
    implementation "nl.komponents.kovenant:kovenant:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-core:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-android:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-combine:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-jvm:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-functional:$KOVENANT_VERSION"

    implementation 'org.zeroturnaround:zt-zip:1.12'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'org.slf4j:slf4j-nop:1.7.25'

    //    implementation 'net.theluckycoder.materialchooser:materialchooser:1.1.4'
    implementation 'com.duolingo.open:rtl-viewpager:1.0.3'
    implementation 'com.commonsware.cwac:merge:1.1.2'
    implementation 'com.github.abacritt:sectionedmergeadapter:0.2'
    implementation 'org.jsoup:jsoup:1.11.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}
